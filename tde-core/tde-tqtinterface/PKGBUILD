# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tqtinterface
pkgver=14.0.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://trinity.pearsoncomputing.net"
license=('GPL2')
groups=('tde-core')
pkgdesc="TQt interface for the TDE desktop"
depends=('tde-tqt3' 'glu')
makedepends=('pkgconfig' 'cmake')
provides=('tqtinterface')
conflicts=('kdemod3-tqtinterface' 'trinity-tqtinterface')
replaces=('trinity-tqtinterface')
# makeopts=('libtool' '!emptydirs' '!strip')
options=('staticlibs' 'libtool' '!emptydirs' '!strip')

source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/dependencies/tqtinterface-R${pkgver}.tar.bz2"
        'tqtpc-location.patch')

md5sums=('b1d086bbcc0cd62dfbb5833ff0af71a2'
         'c15745c944d98cb7c4cb553f0ca421d4')

install='trinity-tqtinterface.install'

_prefix=/usr
_tdedir=/opt/trinity

build() {
   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake "../dependencies/tqtinterface" \
     -DCMAKE_INSTALL_PREFIX=${_prefix} \
     -DQT_PREFIX_DIR=${QTDIR} \
     -DQT_INCLUDE_DIR=${QTDIR}/include \
     -DMOC_EXECUTABLE=${QTDIR}/bin/moc \
     -DQT_VERSION=3

   make $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   make -j1 DESTDIR="${pkgdir}" install

   # Create and install ld.so.conf.d file
   install -d -m755 $pkgdir/etc/ld.so.conf.d/
   echo ${_tdedir}/lib > $pkgdir/etc/ld.so.conf.d/trinity.conf
   echo ${_tdedir}/lib/trinity >> $pkgdir/etc/ld.so.conf.d/trinity.conf
}
