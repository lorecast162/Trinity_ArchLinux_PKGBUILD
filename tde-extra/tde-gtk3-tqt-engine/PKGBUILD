# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-gtk3-tqt-engine
pkgver=14.0.5
pkgrel=1
pkgdesc="gtk3 tqt engine"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/applications/gtk3-tqt-engine'
license=('GPL')
groups=('tde-extra')
depends=('tde-tdebase' 'gtk3')
makedepends=('pkgconfig' 'cmake')
#provides=('tdemultimedia')
#conflicts=('gtk3-tqt-engine')
#replaces=('gtk3-tqt-engine')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/applications/gtk3-tqt-engine-R${pkgver}.tar.bz2")
md5sums=('76b078e7afcd6a8e683efa924fcfad2e')
# install=''

build() {
  ## Generate config files and update with autoreconf
  cd ${srcdir}/applications/${pkgname#*-}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."

  ./configure --prefix=${TDEDIR}

  ### PATCH: This allows the package to compile due to some odd reason its not finding tqt-mt, Remove LD_LIBRARY_PATH
  ###        if fixed on future versions
  LD_LIBRARY_PATH=$TQTDIR/lib make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/applications/${pkgname#*-}
  
  make -j1 DESTDIR="${pkgdir}" install
}
